# OAC setting
resource "aws_cloudfront_origin_access_control" "oac" {
    name                              = "currency-tracker-oac"
    origin_access_control_origin_type = "s3"
    signing_behavior                  = "always"
    signing_protocol                  = "sigv4"
}

# CloudFront Distribution setting
resource "aws_cloudfront_distribution" "s3_distribution" {
    origin {
        domain_name = aws_s3_bucket.frontend.bucket_regional_domain_name
        origin_access_control_id = aws_cloudfront_origin_access_control.oac.id
        origin_id                = "S3-Frontend"
    }

    enabled = true
    is_ipv6_enabled = true
    default_root_object = "index.html"

    # No alias
    # aliases = 

    default_cache_behavior {
        allowed_methods  = ["GET", "HEAD"]
        cached_methods   = ["GET", "HEAD"]
        target_origin_id = "S3-Frontend"

        forwarded_values {
            query_string = false
            cookies {
                forward = "none"
            }
        }

        viewer_protocol_policy = "redirect-to-https"
    }

    custom_error_response {
        error_code = 404
        response_code = 200
        response_page_path = "/index.html"
    }

    restrictions {
        geo_restriction {
            restriction_type = "none"
        }
    }

    viewer_certificate {
        cloudfront_default_certificate = true
    }
}

# 3. S3 Bucket Policy (CloudFront가 S3에 접근할 수 있게 허용)
resource "aws_s3_bucket_policy" "frontend_policy" {
    bucket = aws_s3_bucket.frontend.id
    policy = jsonencode({
        Version = "2012-10-17"
        Statement = [
        {
            Sid       = "AllowCloudFrontServicePrincipal"
            Effect    = "Allow"
            Principal = {
                Service = "cloudfront.amazonaws.com"
            }
            Action   = "s3:GetObject"
            Resource = "${aws_s3_bucket.frontend.arn}/*"
            Condition = {
            StringEquals = {
                "AWS:SourceArn" = aws_cloudfront_distribution.s3_distribution.arn
            }
            }
        }
        ]
    })    
}